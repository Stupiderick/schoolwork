<!DOCTYPE HTML>
<html lang="en">
<head>
<title>Normal</title>
<meta charset="utf-8">
</head>

<script id="shader-vs" type="x-shader/x-vertex">
attribute vec3 aVertexPosition;
attribute vec3 aVertexNormal;
attribute vec3 aVertexTangent;
attribute vec3 aVertexBitangent;
attribute vec2 aTextureCoord;

uniform mat4 uMVMatrix;
uniform mat4 uPMatrix;
uniform mat4 uNMatrix;
uniform vec3 uLightPosition;

varying vec2 vTextureCoord;
varying vec3 vTangentLightDir;
varying vec3 vTangentEyeDir;


void main(void) {
    vec4 vertexPositionEye4 = uMVMatrix * vec4(aVertexPosition, 1.0);
    vec3 aPosition = vertexPositionEye4.xyz / vertexPositionEye4.w;

    vec3 normal = normalize(vec3(uNMatrix * vec4(aVertexNormal,1.0)));
    vec3 tangent = normalize(vec3(uNMatrix * vec4(aVertexTangent,1.0)));
    vec3 bitangent = normalize(vec3(uNMatrix * vec4(aVertexBitangent, 1.0)));

    //tangent-bitangent-normal matrix.
    mat3 tbnMatrix = mat3(
        tangent.x, bitangent.x, normal.x,
        tangent.y, bitangent.y, normal.y,
        tangent.z, bitangent.z, normal.z
    );

    //light direction, from light position to vertex
    vec3 lightDirection = normalize(uLightPosition - aPosition);

    //eye direction, from camera position to vertex
    vec3 viewVectorEye = -aPosition;

    //Final vertex position
    gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
    vTextureCoord = aTextureCoord;
    vTangentLightDir = tbnMatrix * lightDirection;
    vTangentEyeDir = tbnMatrix * viewVectorEye;
}
</script>

<script id="shader-fs" type="x-shader/x-fragment">
precision mediump float;

varying vec2 vTextureCoord;
varying vec3 vTangentLightDir;
varying vec3 vTangentEyeDir;

uniform vec4 uDiffuseMatColor;
uniform vec4 uAmbientMatColor;
uniform vec4 uSpecularLightColor;
uniform vec4 uAmbientLightColor;
uniform vec4 uDiffuseLightColor;
uniform vec4 uSpecularMatColor;
uniform sampler2D uSampler;

void main(void) {
    // get the normal vector per fragment for normal mapping.
    vec3 pixelNormal = normalize(2.0 * (texture2D(uSampler, vTextureCoord).rgb - 0.5));

    // light vector in tangent space.
    vec3 lightDirection = normalize(vTangentLightDir);
    float diffuseLightWeightning = max(dot(pixelNormal, lightDirection), 0.0);

    // eye vector in tangent space.
    vec3 viewVectorEye = normalize(vTangentEyeDir);
    vec3 reflectDir = reflect(-lightDirection, pixelNormal);

    // halfway vector in tangent space.
    vec3 halfway = normalize(lightDirection + viewVectorEye); // halfway vector
    float hdotn = max(dot(halfway, pixelNormal), 0.0);

    float specularLightWeightning = pow(hdotn, 100.0);

    // specular colors.
    vec4 Is = (uSpecularLightColor * uSpecularMatColor) * specularLightWeightning;
    // ambient colors.
    vec4 Ia = uAmbientLightColor * uAmbientMatColor;
    // diffuse colors.
    vec4 Id = uDiffuseLightColor * uDiffuseMatColor * diffuseLightWeightning;

    gl_FragColor = Ia + Id + Is;
}
</script>

<script src="gl-matrix-min.js"></script>
<script src="webgl-utils.js"></script>
<script src="normal.js"></script>
<script src="simpleModeling.js"></script>
<body onload="startup();">
    <canvas id="myGLCanvas" width="750" height="750"></canvas>
    <form id="input_form">
        <fieldset>
            <b>Instructions: </b><br/>
            <p>
                Press <mark>left arrow</mark> on keyboard to rotate the sphere counterclockwisely;
                or press <mark>right arrow</mark> to rotate it clockwisely. <br />
                Press <mark>up arrow</mark> to rotate it upwards;
                or press <mark>down arrow</mark> to rotate it downwards. <br />
            </p>
        </fieldset>
    </form>
</body>
</html>
